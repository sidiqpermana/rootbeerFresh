apply plugin: 'com.android.application'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.4'

        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    }
}

apply plugin: 'jacoco-android'

android {
    compileSdkVersion compileSdkVer
    buildToolsVersion buildToolsVer

    defaultConfig {
        applicationId "com.kimchangyoun.rootbeerFresh.sample"
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode 10
        versionName "0.0.10"
    }

    //check if the keystore details are defined in gradle.properties (this is so the key is not in github)
    if (project.hasProperty("ROOTBEER_SAMPLE_STORE")) {
        signingConfigs {
            //from ~/user/.gradle/gradle.properties
            release {
                storeFile file(ROOTBEER_SAMPLE_STORE)
                keyAlias ROOTBEER_SAMPLE_KEY
                storePassword ROOTBEER_SAMPLE_STORE_PASS
                keyPassword ROOTBEER_SAMPLE_KEY_PASS
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        release {
            if (project.hasProperty("ROOTBEER_SAMPLE_STORE")) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(':LibRootbeerFresh')
    implementation "com.android.support:appcompat-v7:$supportLibVer"
    implementation "com.android.support:design:$supportLibVer"
    implementation 'com.nineoldandroids:library:2.4.0'
    implementation 'uk.co.barbuzz:beerprogressview:0.0.6'
}
